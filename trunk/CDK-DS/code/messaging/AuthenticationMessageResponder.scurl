||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{define-class public AuthenticationMessageResponder {inherits MessageResponder}

  {constructor public {default
                          agent:#MessageAgent,
                          message:IMessage,
                          channel:Channel
                      }
    {construct-super agent, message, channel = channel}
  }

  {method public {result msg:IMessage}:void
    def cmd = self.message asa CommandMessage
    def channel = self.channel

    set channel.authenticating? = false
    {if cmd.operation == CommandMessage.LOGIN_OPERATION then
        {if-debuggable
            {output "Login successful"}} ||""
        set channel.authenticated? = true
     else
        {if-debuggable
            {output "Logout successful"}} ||""
        set channel.authenticated? = false
    }
  }

  {method public {status msg:IMessage}:void
    set self.channel.authenticating? = false
    set self.channel.authenticated? = false

    {if self.agent != null and
        {self.agent.has-pending-request-for-message self.message}
     then
        {self.agent.fault
            msg asa ErrorMessage,
            self.message
        }
     else
        def err-msg = {ErrorMessage}
        def cfe = {ChannelFaultEvent
                      channel = self.channel,
                      code = "Channel.Authentication.Error", ||""
                      level = "warn", ||""
                      description = err-msg.fault-string,
                      root-cause = err-msg
                  }
        {self.channel.enqueue-event cfe}
    }
  }
}

