||| Copyright (C) 1998-2008, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{define-proc package {http-url? loc:Url}:bool
    {return
        {loc.stem.name.prefix? "http"}}
}
{doc-next
    {purpose
        If {param loc} is not an http {docref Url} return it,
        otherwise copy it to {ctext "curl://user-data/<path>"},
        and return that location.
    }
}
{define-proc public {user-data-copy-of
                        loc:Url,
                        path:String
                    }:Url
    {if not {http-url? loc} then
        {return loc}}
    def uloc = "curl://user-data/" & path
    def cloc = {{url uloc}.canonicalize}
    {return {local-copy-of loc, cloc}}
}
{doc-next
    {purpose
        If {param loc} is not an http {docref Url} return it,
        otherwise copy it to {ctext "curl://local-data/<path>"},
        and return that location.
    }
}
{define-proc public {local-data-copy-of
                        loc:Url,
                        path:String
                    }:Url
    {if not {http-url? loc} then
        {return loc}}
    || TODO: needed only when libqlite can run without privilege
    || -- request-local-data-permission
    || -- fallback to get-temp-filename, choose-file
    def uloc = "curl://local-data/" & path
    def cloc = {{url uloc}.canonicalize}
    {return {local-copy-of loc, cloc}}
}
{define-proc package {local-copy-of
                        loc:Url,
                        dir:Url
                    }:Url
    {if not {http-url? loc} then
        {return loc}}
    {assert dir.local-filename !=  null}
    def file = {dir.concat loc.filename}
    || TODO: check validators: timestamp and content-hash
    {if not {local-file-exists? file} then
        {create-Directory dir,
            create-missing-parents? = true,
            error-if-exists? = false}
        {with-local-file-lock file do
            {copy loc, file}}
    }
    {return file}
}
{define-proc package {local-SharedLibrary name:String}:SharedLibrary
    || manifest specifies library location
    || - qualified by platform
    def mloc =
        {platform-switch
         case "win32" do
            {manifest-url "file", name,
                platform = "win32"}
         case "linux" do
            {manifest-url "file", name,
                platform = "linux"}
         else
            || TODO: for MAC also
            {error {lmessage SharedLibrary {value name} not available for this platform.}}
        }
    || if HTTP, copy library to LocalFile
    def loc = 
        {if {http-url? mloc} then
            || TODO: dguide recommends "curl://common/"
            {user-data-copy-of mloc, name}
         else mloc}
    {return 
        {SharedLibrary.from-url loc}}
}
