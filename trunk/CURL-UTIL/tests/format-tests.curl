||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An Open Curl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{curl 7.0, 6.0 applet}
{applet manifest = "manifest.mcurl",
    {compiler-directives careful? = true}
}


{import * from COM.CURL.UTIL.BIGNUM}

{define-proc {assert-dec-ok d:BigDecimal}:void
    def sd = {d.to-String}
    def dd = sd asa BigDecimal
    {assert {dd.equal? d}}
    {assert {d.equal? dd}}
    {assert {dd.unscaled-value.equal? d.unscaled-value}}
    {assert dd.scale == d.scale}
}

{define-proc {add-interest prin:BigDecimal, rate:BigDecimal, years:int}:BigDecimal
    {for i = 1 to years do
        def interest = {prin.multiply rate, scale = prin.scale}
        set prin = {prin.add interest}
    }
    {return prin}
}


{let package rand:Random = {LinearRandom seed = 12345}}

{define-proc {dec-rand max-val:BigDecimal, scale:int = 0}:BigDecimal
    def rf = ({rand.next-float} asa double) + 1e-6 * {rand.next-float}
    {return {max-val.multiply rf, scale = scale}}
}


{define-proc {interest-test n:int}:void
    {for i = 1 to n do
        def prin = {dec-rand 99999999, scale = 2}
        def rate = {dec-rand 0.15, scale = 4}
||--        def prin:BigDecimal = 499999999
||--        def rate:BigDecimal = "0.08"
        def end-prin = {add-interest prin, rate, 10}
        {assert-dec-ok prin}
        {assert-dec-ok rate}
        {assert-dec-ok end-prin}
    }
}

{CommandButton
    label = "Interest test",
    {on Action do
        {with-busy-cursor
            {interest-test 30000}
            {popup-message "Done!"}
        }
    }
}
