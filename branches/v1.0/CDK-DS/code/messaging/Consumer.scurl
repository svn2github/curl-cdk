||| Copyright (C) 1998-2008, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{define-class public Consumer {inherits AbstractConsumer}
  field protected _selector:String = ""
  field protected _subtopic:String = ""

  {constructor public {default
                          endpoint:#Url = null,
                          destination:String = "",
                          ...
                      }
    {construct-super {splice ...}}
    set self.endpoint = endpoint
    set self._destination = destination
  }

  {getter public {selector}:String
    {return self._selector}
  }
  {setter public {selector s:String}:void
    {if self._selector != s then
        let reset-subscription?:bool = false
        {if self.subscribed? then
            {self.unsubscribe}
            set reset-subscription? = true
        }
        set self._selector = s
        {if reset-subscription? then
            {self.subscribe client-id = self.client-id}
        }
        || TODO: Fire PropertyChangeEvent
    }
  }

  {getter public {subtopic}:String
    {return self._subtopic}
  }
  {setter public {subtopic s:String}:void
    {if self._subtopic != s then
        let reset-subscription?:bool = self.subscribed?
        {if reset-subscription? then
            {self.unsubscribe}
        }
        set self._subtopic = s
        {if reset-subscription? then
            {self.subscribe}
        }
    }
  }

  {method public {internal-send
                     message:IMessage,
                     wait-for-client-id?:bool = true
                 }:void
    {if not self.subtopic.empty? then
        set message.headers[AsyncMessage.SUBTOPIC_HEADER] = self.subtopic
    }
    {if not self._selector.empty? then
        set message.headers[CommandMessage.SELECTOR_HEADER] = self._selector
    }
    {super.internal-send message, wait-for-client-id? = wait-for-client-id?}
  }
}
